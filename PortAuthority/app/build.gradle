apply plugin: 'com.android.application'
apply plugin: "org.sonarqube"

sonarqube {
    properties {
        property "sonar.projectName", "Port Authority"
        property "sonar.projectKey", "portauthority"
        property "sonar.projectVersion", "1.0"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "."
        property "sonar.exclusions", "build/**,**/*.png,**/*.psd"
        property "sonar.import_unknown_files", true
        property "sonar.android.lint.report", "./build/outputs/lint-results.xml"
    }
}

android {
    signingConfigs {
        debug {
            storePassword 'q12345'
            keyPassword 'q12345'
        }
    }
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 55
        versionName "2.2.8"
        applicationId "com.aaronjwood.portauthority"
        setProperty("archivesBaseName", "PortAuthority-$versionName")
        signingConfig signingConfigs.debug
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
//            useProguard false
            android.enableR8 false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.pro'
        }
    }

    flavorDimensions "main"

    productFlavors {
        free {
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            dimension "main"
        }
        donate {
            applicationIdSuffix ".donate"
            versionNameSuffix "-donate"
            dimension "main"
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.squareup.okhttp3:okhttp:4.3.1'
    implementation 'jcifs:jcifs:1.3.17'
    implementation 'dnsjava:dnsjava:3.0.0-next.1'
    //This does absolutely nothing
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}
